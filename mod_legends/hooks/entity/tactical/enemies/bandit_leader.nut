::mods_hookExactClass("entity/tactical/enemies/bandit_leader", function(o)
{
	local create = o.create;
	o.create = function ()
	{
		create();
		this.m.AIAgent = this.new("scripts/ai/tactical/agents/legend_bandit_melee_agent_less_flanking");
		this.m.AIAgent.setActor(this);
		this.m.OnDeathLootTable.extend([
			[1, "scripts/items/misc/legend_masterwork_fabric"],
			[0.7, "scripts/items/misc/legend_masterwork_metal"],
			[0.5, "scripts/items/misc/legend_masterwork_tools"]
		]);
	}

	o.onInit = function ()
	{
		this.human.onInit();
		local b = this.m.BaseProperties;
		b.setValues(this.Const.Tactical.Actor.BanditLeader);
		b.IsSpecializedInSwords = true;
		b.IsSpecializedInAxes = true;
		b.IsSpecializedInMaces = true;
		b.IsSpecializedInFlails = true;
		b.IsSpecializedInPolearms = true;
		b.IsSpecializedInThrowing = true;
		b.IsSpecializedInHammers = true;
		b.IsSpecializedInSpears = true;
		b.IsSpecializedInCleavers = true;
		b.IsSpecializedInDaggers = true;
		this.m.ActionPoints = b.ActionPoints;
		this.m.Hitpoints = b.Hitpoints;
		this.m.CurrentProperties = clone b;
		this.setAppearance();
		this.getSprite("socket").setBrush("bust_base_bandits");
		local dirt = this.getSprite("dirt");
		dirt.Visible = true;
		dirt.Alpha = this.Math.rand(150, 255);
		this.setArmorSaturation(0.85);
		this.getSprite("shield_icon").setBrightness(0.85);
		::Legends.Perks.grant(this, ::Legends.Perk.Captain);
		::Legends.Perks.grant(this, ::Legends.Perk.ShieldExpert);
		::Legends.Perks.grant(this, ::Legends.Perk.Brawny);
		::Legends.Perks.grant(this, ::Legends.Perk.CoupDeGrace);
		::Legends.Perks.grant(this, ::Legends.Perk.LegendSmackdown);
		::Legends.Perks.grant(this, ::Legends.Perk.QuickHands);
		::Legends.Perks.grant(this, ::Legends.Perk.NineLives);
		::Legends.Perks.grant(this, ::Legends.Perk.Rotation);
		::Legends.Perks.grant(this, ::Legends.Perk.Recover);

		if (::Legends.isLegendaryDifficulty())
		{
			::Legends.Perks.grant(this, ::Legends.Perk.BattleForged);
			::Legends.Perks.grant(this, ::Legends.Perk.LegendComposure);
			::Legends.Perks.grant(this, ::Legends.Perk.Relentless);
			::Legends.Perks.grant(this, ::Legends.Perk.Dodge);
			::Legends.Perks.grant(this, ::Legends.Perk.LegendStrengthInNumbers);
			::Legends.Perks.grant(this, ::Legends.Perk.Underdog);
			::Legends.Traits.grant(this, ::Legends.Trait.Fearless);
			::Legends.Actives.grant(this, ::Legends.Active.RallyTheTroops);
		}
	}

	o.onAppearanceChanged = function ( _appearance, _setDirty = true )
	{
		this.actor.onAppearanceChanged(_appearance, false);
		this.setArmorBrightness(0.9);
		this.setDirty(true);
	}

	o.assignRandomEquipment = function ()
	{
		if (this.m.Items.getItemAtSlot(this.Const.ItemSlot.Mainhand) == null)
		{
			local weapons = [
				"weapons/noble_sword",
				"weapons/fighting_axe",
				"weapons/warhammer",
				"weapons/fighting_spear",
				"weapons/winged_mace",
				"weapons/arming_sword",
				"weapons/military_cleaver"
			];

			if (this.m.Items.getItemAtSlot(this.Const.ItemSlot.Offhand) == null)
			{
				weapons.extend([
					"weapons/greatsword",
					"weapons/greataxe",
					"weapons/legend_swordstaff",
					"weapons/legend_longsword",
					"weapons/warbrand",
					"weapons/legend_estoc",
					"weapons/legend_battle_glaive",
				]);
			}

			this.m.Items.equip(this.new("scripts/items/" + weapons[this.Math.rand(0, weapons.len() - 1)]));
		}

		if (this.m.Items.getItemAtSlot(this.Const.ItemSlot.Offhand) == null)
		{
			local shields = [
				"shields/wooden_shield",
				"shields/heater_shield",
				"shields/kite_shield"
			];
			this.m.Items.equip(this.new("scripts/items/" + shields[this.Math.rand(0, shields.len() - 1)]));
		}

		if (this.Math.rand(1, 100) <= 35)
		{
			local weapons = [
				"weapons/throwing_axe",
				"weapons/javelin"
			];
			this.m.Items.addToBag(this.new("scripts/items/" + weapons[this.Math.rand(0, weapons.len() - 1)]));
		}

		if (this.m.Items.hasEmptySlot(this.Const.ItemSlot.Body) && this.m.Items.hasEmptySlot(this.Const.ItemSlot.Head))
		{
			local armor = [
				[1, ::Legends.Armor.Standard.coat_of_plates],
				[1, ::Legends.Armor.Standard.coat_of_scales],
				[1, ::Legends.Armor.Standard.heavy_lamellar_armor],
				[1, ::Legends.Armor.Standard.footman_armor],
				[1, ::Legends.Armor.Standard.brown_hedgeknight_armor],
				[1, ::Legends.Armor.Standard.northern_mercenary_armor_02],
				[1, ::Legends.Armor.Standard.lamellar_harness],
				[1, ::Legends.Armor.Standard.reinforced_mail_hauberk],
				[1, ::Legends.Armor.Standard.leather_scale_armor],
				[1, ::Legends.Armor.Standard.light_scale_armor]
			];

			local helmet = [
				[1, ::Legends.Helmet.Standard.closed_mail_coif],
				[1, ::Legends.Helmet.Standard.padded_kettle_hat],
				[1, ::Legends.Helmet.Standard.kettle_hat_with_closed_mail],
				[1, ::Legends.Helmet.Standard.kettle_hat_with_mail],
				[1, ::Legends.Helmet.Standard.padded_flat_top_helmet],
				[1, ::Legends.Helmet.Standard.nasal_helmet_with_mail],
				[1, ::Legends.Helmet.Standard.flat_top_with_mail],
				[1, ::Legends.Helmet.Standard.padded_nasal_helmet],
				[1, ::Legends.Helmet.Standard.bascinet_with_mail]
			];

			local outfits = [
				[1, ::Legends.Outfit.red_bandit_leader_outfit_00]
			];

			foreach( item in this.Const.World.Common.pickOutfit(outfits, armor, helmet) )
			{
				this.m.Items.equip(item)
			}
			return;
		}

		if (this.m.Items.getItemAtSlot(this.Const.ItemSlot.Body) == null)
		{
			local armor = [
				[1, ::Legends.Armor.Standard.reinforced_mail_hauberk],
				[2, ::Legends.Armor.Standard.heavy_lamellar_armor],
				[2, ::Legends.Armor.Standard.bandit_armor_heavy],
				[1, ::Legends.Armor.Standard.footman_armor],
				[1, ::Legends.Armor.Standard.leather_scale_armor],
				[1, ::Legends.Armor.Standard.light_scale_armor],
				[1, ::Legends.Armor.Standard.red_bandit_leader_armor]
			];

			this.m.Items.equip(this.Const.World.Common.pickArmor(armor))
		}

		if (this.m.Items.getItemAtSlot(this.Const.ItemSlot.Head) == null)
		{
			local item = this.Const.World.Common.pickHelmet([
				[1, ::Legends.Helmet.Standard.closed_mail_coif],
				[1, ::Legends.Helmet.Standard.padded_kettle_hat],
				[1, ::Legends.Helmet.Standard.kettle_hat_with_closed_mail],
				[1, ::Legends.Helmet.Standard.kettle_hat_with_mail],
				[1, ::Legends.Helmet.Standard.padded_flat_top_helmet],
				[1, ::Legends.Helmet.Standard.nasal_helmet_with_mail],
				[1, ::Legends.Helmet.Standard.flat_top_with_mail],
				[1, ::Legends.Helmet.Standard.padded_nasal_helmet],
				[1, ::Legends.Helmet.Standard.bascinet_with_mail],
				[1, ::Legends.Helmet.Standard.red_bandit_leader_helmet]
			]);
			if (item != null)
			{
				this.m.Items.equip(item);
			}
		}
	}

	o.makeMiniboss = function ()
	{
		if (!this.actor.makeMiniboss())
		{
			return false;
		}

		this.getSprite("miniboss").setBrush("bust_miniboss");
		local shields = clone this.Const.Items.NamedShields;
		shields.extend([
			"shields/named/named_bandit_kite_shield",
			"shields/named/named_bandit_heater_shield"
		]);
		local r = this.Math.rand(1, 100);

		if (r < 30)
		{
			local namedWeaponArray = clone ::Const.Items.NamedMeleeWeapons;		// Temporary solution to prevent these minibosses from spawning with bad or incombatible weapons
			::MSU.Array.remove(namedWeaponArray, "weapons/named/named_dagger");
			::MSU.Array.remove(namedWeaponArray, "weapons/named/legend_named_parrying_dagger");
			::MSU.Array.remove(namedWeaponArray, "weapons/named/legend_named_shovel");
			::MSU.Array.remove(namedWeaponArray, "weapons/named/legend_named_sickle");
			::MSU.Array.remove(namedWeaponArray, "weapons/named/named_battle_whip");
			this.getItems().equip(this.Const.World.Common.pickItem(namedWeaponArray.map(@(_it) [1, _it]), "scripts/items/"));
		}
		else if (r < 60)
		{
			local named = this.Const.Items.NamedArmors;
			local weightName = this.Const.World.Common.convNameToList(named);
			this.m.Items.equip(this.Const.World.Common.pickArmor(weightName));
		}
		else if (r < 90)
		{
			local named = this.Const.Items.NamedHelmets;
			local weightName = this.Const.World.Common.convNameToList(named);
			this.m.Items.equip(this.Const.World.Common.pickHelmet(weightName));
		}
		else
		{
			this.m.Items.equip(this.new("scripts/items/" + shields[this.Math.rand(0, shields.len() - 1)]));
		}

		::Legends.Perks.grant(this, ::Legends.Perk.Underdog);
		return true;
	}
});
